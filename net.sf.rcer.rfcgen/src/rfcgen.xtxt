/**
 * Copyright (c) 2008 The RCER Development Team.
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html. If redistributing this code,
 * this entire header must remain intact.
 *
 * $Id$
 */

GenSpec:
	"package" packageName=STRING
	(structures += Structure)*
	(functionModules += FunctionModule)*;

// ====== Structures ==============================================================================

Structure:
	"structure" name=STRING "{"
		mapping = StructureMapping
	"}";

StructureMapping:
	StructurePOJOMapping | StructureEMFMapping;

// --- POJO mapping -------------------------------------------------------------------------------

StructurePOJOMapping:
	 "class" className=ID "{"
		(fields += StructureFieldPOJOMapping)*
	"}";

StructureFieldPOJOMapping:
	"field" name=STRING "=" type=ID attribute=ID ("comment" comment=STRING)? ";";

// --- EMF mapping --------------------------------------------------------------------------------

StructureEMFMapping:
	"EClass" className=ID "{"
		(fields += StructureFieldEMFMapping)*
	"}";

StructureFieldEMFMapping:
	"field" name=STRING "=" attribute=ID ";";

// ====== Function Modules ========================================================================

FunctionModule: 
	"function" "module" name=STRING "{"
		mapping=FunctionModuleMapping
	"}";
	
FunctionModuleMapping: 
	FunctionModulePOJOMapping | FunctionModuleEMFMapping;

// --- POJO mapping -------------------------------------------------------------------------------

FunctionModulePOJOMapping: 
	FunctionModulePOJOCallMapping | 
	FunctionModulePOJORequestResponseMapping;

FunctionModulePOJOCallMapping: 
	"class" className=ID "{"
		(parameters += FunctionModulePOJOParameterMapping)*
	"}";

FunctionModulePOJORequestResponseMapping: 
	"request" "class" requestClassName=ID "{"
		(requestParameters += FunctionModulePOJOParameterMapping)*
	"}"
	"response" "class" responseClassName=ID "{"
		(responseParameters += FunctionModulePOJOParameterMapping)*
	"}";

FunctionModulePOJOParameterMapping: 
	FunctionModulePOJOImportingParameter | 
	FunctionModulePOJOExportingParameter | 
	FunctionModulePOJOChangingParameter  | 
	FunctionModulePOJOTablesParameter; 

FunctionModulePOJOImportingParameter: 
	"import" ("field"                  name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID) | 
	          isStructure?="structure" name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID) |
	          isTable?="table"         name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID ))
	          ("comment" comment=STRING)? ";";

FunctionModulePOJOExportingParameter: 
	"export" ("field"                  name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID) | 
	          isStructure?="structure" name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID) |
	          isTable?="table"         name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID))
	          ("comment" comment=STRING)? ";";

FunctionModulePOJOChangingParameter: 
	"change" ("field"                  name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID) | 
	          isStructure?="structure" name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID) |
	          isTable?="table"         name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID))
	          ("comment" comment=STRING)? ";";

FunctionModulePOJOTablesParameter:
	"table" name=STRING "=" (isInactive?="inactive" | type=ID attribute=ID) ("comment" comment=STRING)? ";";

// --- EMF mapping --------------------------------------------------------------------------------

FunctionModuleEMFMapping: 
	FunctionModuleEMFCallMapping | 
	FunctionModuleEMFRequestResponseMapping;

FunctionModuleEMFCallMapping: 
	"EClass" className=ID "{"
		(parameters += FunctionModuleEMFParameterMapping)*
	"}";

FunctionModuleEMFRequestResponseMapping: 
	"request" "EClass" requestClassName=ID "{"
		(requestParameters += FunctionModuleEMFParameterMapping)*
	"}"
	"response" "EClass" responseClassName=ID "{"
		(responseParameters += FunctionModuleEMFParameterMapping)*
	"}";

FunctionModuleEMFParameterMapping: 
	FunctionModuleEMFImportingParameter | 
	FunctionModuleEMFExportingParameter | 
	FunctionModuleEMFChangingParameter  | 
	FunctionModuleEMFTablesParameter; 

FunctionModuleEMFImportingParameter: 
	"import" ("field"                  name=STRING "=" (isInactive?="inactive" | attribute=ID) | 
	          isStructure?="structure" name=STRING "=" (isInactive?="inactive" | attribute=ID) |
	          isTable?="table"         name=STRING "=" (isInactive?="inactive" | attribute=ID)) ";";

FunctionModuleEMFExportingParameter: 
	"export" ("field"                  name=STRING "=" (isInactive?="inactive" | attribute=ID) | 
	          isStructure?="structure" name=STRING "=" (isInactive?="inactive" | attribute=ID) |
	          isTable?="table"         name=STRING "=" (isInactive?="inactive" | attribute=ID)) ";";

FunctionModuleEMFChangingParameter: 
	"change" ("field"                  name=STRING "=" (isInactive?="inactive" | attribute=ID) | 
	          isStructure?="structure" name=STRING "=" (isInactive?="inactive" | attribute=ID) |
	          isTable?="table"         name=STRING "=" (isInactive?="inactive" | attribute=ID)) ";";

FunctionModuleEMFTablesParameter:
	"table" name=STRING "=" (isInactive?="inactive" | attribute=ID) ";";
